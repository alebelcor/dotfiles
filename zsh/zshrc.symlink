#!/usr/bin/env zsh

# Set shortcut to dotfiles path
ZSH="$HOME/projects/dotfiles"
export ZSH

# Load `.localrc` if it exists.
# A place to store private/custom shell information like environmental variables.
if test -f "$HOME/.localrc"
then
	source "$HOME/.localrc"
fi

# All of our ZSH files.
typeset -U config_files
config_files=("$ZSH"/**/*.zsh)

# Load the path files.
for file in ${(M)config_files:#*/path.zsh}
do
	source "$file"
done

# Load everything except the path and completion files.
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
	source "$file"
done

# Initialize autocomplete here, otherwise functions won't be loaded.
autoload -Uz compinit

# Only bother with rebuilding, auditing, and compiling the `compinit`
# file once a whole day has passed. The `-C` flag bypasses both the
# check for rebuilding the dump file and the usual call to `compaudit`.
setopt EXTENDEDGLOB
for dump in "${HOME}"/.zcompdump(#qN.m1);
do
	compinit

	if [[ -s "$dump" && (! -s "$dump.zwc" || "$dump" -nt "$dump.zwc") ]]; then
		zcompile "$dump"
	fi
done
unsetopt EXTENDEDGLOB
compinit -C

# Load every completion, after autocomplete loads.
for file in ${(M)config_files:#*/completion.zsh}
do
	source "$file"
done

unset config_files
